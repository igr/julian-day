plugins {
	id 'java-library'
	id 'maven-publish'
	id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

repositories {
	mavenCentral()
}

group = 'org.jodd'
version = '7.1.0'

rootProject.description = 'Julian Day'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
	withSourcesJar()
	withJavadocJar()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.+'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.hamcrest:hamcrest:2.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Debug-Info': 'on'
        )
    }
}

javadoc {
	options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = 'julian-day'

            from components.java

			pom {
				name = 'Julian Day'
				description = 'Julian Day'
				url = 'https://github.com/igr/julian-day'
				licenses {
					license {
						name = 'BSD-2-Clause'
						url = 'https://github.com/igr/julian-day/blob/master/LICENSE'
					}
				}
				developers {
					developer {
						id = 'igr'
						name = 'Igor SpasiÄ‡'
						email = 'oblac@igo.rs'
						timezone = '+1'
					}
				}
				scm {
					url = 'https://github.com/igr/julian-day.git'
					connection = 'scm:git:git://github.com/igr/julian-day.git'
					developerConnection = 'scm:git:ssh://git@github.com/igr/julian-day.git'
				}
			}
		}
	}
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

// must be after publishing
signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
